plugins {
    id 'java-library'
    id 'jacoco'
}

group 'uk.ac.ebi.ge'
version '4.0.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    jcenter()
    // For patched version of SolrJ (see dependencies below)
    mavenLocal()
    maven {
        url "http://bar.ebi.ac.uk:8081/artifactory/repo/"
    }
}

test {
    if (project.hasProperty('testResultsPath')) {
        reporting.baseDir = "$buildDir/reports/" + project.property('testResultsPath')
        testResultsDirName = "test-results/" + project.property('testResultsPath')
    }

    if (project.hasProperty('excludeTests')) {
        exclude project.property('excludeTests')
    }

    useJUnitPlatform()
    maxHeapSize = '2048m'
}

if (!hasProperty('buildProfile')) ext.buildProfile = 'dev'
apply from: "build-profiles/profile-${buildProfile}.gradle"

processTestResources {
    filesMatching(['*.properties', '*.xml']) {
        expand(
                jdbcUrl: jdbcUrl,
                jdbcUsername: jdbcUsername,
                jdbcPassword: jdbcPassword,
                dataFilesLocation: dataFilesLocation,
                projectBuildDirectory: buildDir,
                testLogs: testLogs,
                zkHost: zkHost,
                zkPort: zkPort,
                solrHost: solrHost,
                solrPort: solrPort
        )
    }
}

dependencies {
    implementation 'org.springframework:spring-context:5.1.5.RELEASE'
    implementation 'org.springframework:spring-jdbc:5.1.5.RELEASE'
    implementation 'org.springframework:spring-web:5.1.5.RELEASE'
    implementation 'org.springframework:spring-webmvc:5.1.5.RELEASE'
    implementation 'org.springframework:spring-webflux:5.1.5.RELEASE'

    implementation 'io.projectreactor.netty:reactor-netty:0.8.6.RELEASE'
    implementation 'org.reactivestreams:reactive-streams:1.0.2'

    implementation 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.2.200'
    implementation 'org.jetbrains:annotations:17.0.0'

    implementation 'com.google.guava:guava:27.1-jre'
    
    // Use 'api' rather than 'compile' for Android or java-library projects.
    implementation 'com.google.auto.value:auto-value-annotations:1.6.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'

    // jackson-databind is required by RestTemplate ¯\_(ツ)_/¯
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'

    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'org.apache.commons:commons-math:2.2'
    implementation 'org.apache.commons:commons-configuration2:2.4'
    implementation 'commons-collections:commons-collections:3.1'
    
    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    // Needs to match Tomcat version https://tomcat.apache.org/whichversion.html
    implementation 'javax.servlet:javax.servlet-api:3.1.0'

    implementation 'au.com.bytecode:opencsv:2.4'

    implementation 'io.atlassian.util.concurrent:atlassian-util-concurrent:4.0.1'

    implementation 'com.zaxxer:HikariCP:3.3.1'

    implementation 'joda-time:joda-time:2.10.1'

    implementation 'uk.ac.ebi.fg:ae-efo-loader:1.0.5'

    // Nasty bug in HttpClient 4.5.3 (bundled with Solr)
    // https://issues.apache.org/jira/browse/HTTPCLIENT-1831
    implementation 'org.apache.httpcomponents:httpclient:4.5.8'
    // We’re using a patched version of SolrJ in which QueryRequest uses POST instead of GET to avoid erros due to very
    // long (i.e. unparseable) URIs. You need to install this on your local Maven repository: see mavenLocal() above
    implementation ('org.apache.solr:solr-core:7.1.0-QUERY_REQUEST_WITH_POST') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    // solr-core includes/depends on SolrJ
    // implementation 'org.apache.solr:solr-solrj:7.1.0

    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'org.springframework:spring-test:5.1.5.RELEASE'
    testImplementation 'com.h2database:h2:1.4.199'
    testImplementation 'org.mockito:mockito-core:2.25.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.25.1'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.assertj:assertj-guava:3.2.0'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.1'
    testImplementation 'commons-beanutils:commons-beanutils:1.9.3'
    testImplementation 'commons-jxpath:commons-jxpath:1.3'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    testCompile 'junit:junit:4.12'
    testRuntime 'org.junit.vintage:junit-vintage-engine:5.4.1'
}

